@echo off
:: ****************************************
:: Редактировать данный скрипт рекомендуется в программе Notepad++
::
:: Автор скрипта Михаил Медведев aka mihanik
::
:: https://mihanik.net
::
::        Требуется наличие прав администратора: ДА
:: Антивирусная программа должна быть отключена: желательно, но не обязательно
::                                    Замечания: НЕТ
::
:: Описание
::
:: Скрипт установки пакета основных программ на компьютеры Softium.
:: 
:: Это основной скрипт.
:: ====================
:: Если этому скрипту требуется выполнить некоторые действия или установить какую-то программу
:: он вызывает соотвествующий скрипт из папки ".\Scripts"
::
:: Если вам не требуется устанавливать какие-то программы или нет необходимости выполнять некоторые
:: действия по настройке компьютера, просто удалите или задокументируйте часть скрипта, которая это делает.
::
:: ****************************************

:: ****************************************************************************************
:: Проверяем наличие у пользователя админских прав.
:: Если таковых прав нет, завершаем работу скрипта.
:: ****************************************************************************************

SET HasAdminRights=0
FOR /F %%i IN ('WHOAMI /PRIV /NH') DO (
	IF "%%i"=="SeTakeOwnershipPrivilege" SET HasAdminRights=1
)

IF NOT %HasAdminRights%==1 (
	ECHO .
	ECHO Для запуска этого скрипта вам нужно обладать правами "Администратоа"
	ECHO .
	GOTO END
)

:: Глобальная переменная описывающая путь из которого выполняется данный скрипт.
:: !!! Используется во всей сессии CMD !!!
:: Если данная переменная не будет определена, то некоторые дополнительные скрипты 
:: !!!! не смогут быть запущены !!!

set ScriptPath=%~dp0

:: ****************************************************************************************
:: Выводим небольшое меню для установки некоторых параметров выполнения скрипта.
:: Описываем переменные, которые влияют на параметры работы скрипта.
:: ****************************************************************************************

:: Составляем основное меню программы

:: Описываем используемые переменные

:: Настроить аккаунты пользователей
set BUserAccounts=1

:: Отключить обновление Windows
set BDisWinUpdates=1

:: Удалить "лишние" программы Windows
set BDelWinApps=1

:: Ограничить запуск программ
set BGroupPolicy=1

:: Очистить общий рабочий стол
set BClerAllUsersDesktop=1

:: Установить скрипт очистки профиля ребёнка
set BClearSoftiumProfile=1

mode 128,32
color 1f
cls

:StartMenu

echo Выберите параметры настройки рабочего места.
echo.

if %BUserAccounts% equ 1 (
		echo 1 - Настроить аккаунты пользователей - Да
	) else (
		echo 1 - Настроить аккаунты пользователей - Нет
	)

if %BDisWinUpdates% equ 1 (
		echo 2 - Отключить обновление Windows - Да
	) else (
		echo 2 - Отключить обновление Windows - Нет
	)

if %BDelWinApps% equ 1 (
		echo 3 - Удалить "лишние" программы Windows - Да
	) else (
		echo 3 - Удалить "лишние" программы Windows - Нет
	)

if %BGroupPolicy% equ 1 (
		echo 4 - Ограничить запуск программ - Да
	) else (
		echo 4 - Ограничить запуск программ - Нет
	)

if %BClerAllUsersDesktop% equ 1 (
		echo 5 - Очистить общий рабочий стол - Да
	) else (
		echo 5 - Очистить общий рабочий стол - Нет
	)

if %BClearSoftiumProfile% equ 1 (
		echo 6 - Установить скрипт очистки профиля ребёнка - Да
	) else (
		echo 6 - Установить скрипт очистки профиля ребёнка - Нет
	)

echo 7 - Выполнить скрипт с указанными ограничениями

echo 8 - Выполнить скрипт со значениями по-умолчанию

echo 9 - Прервать работу скрипта

CHOICE /C:123456789 /N /T 30 /D 6

cls

:: Настройка учётных записей пользователей на компьютере
if %errorlevel%==1 ( set /a BUserAccounts=1-BUserAccounts )

:: Отключение автоматического обновление системы
if %errorlevel%==2 ( set /a BDisWinUpdates=1-BDisWinUpdates )

:: Удаление "лишних" программ Windows
if %errorlevel%==3 ( set /a BDelWinApps=1-BDelWinApps )

:: Настройка групповой политики на ПК
if %errorlevel%==4 ( set /a BGroupPolicy=1-BGroupPolicy )

:: Удаление файлов с общего рабочего стола
if %errorlevel%==5 ( set /a BClerAllUsersDesktop=1-BClerAllUsersDesktop )

:: установка скрипта очистки профиля пользователя
if %errorlevel%==6 ( set /a BClearSoftiumProfile=1-BClearSoftiumProfile )

:: Запускаем скрипт
if %errorlevel%==7 (goto :MyStart)

:: Устанавливаем значения по умолчанию
if %errorlevel%==8 (goto :SetDefVal)

:: Завершаем работу скрипта
if %errorlevel%==9 (EXIT /B)

GOTO :StartMenu

:SetDefVal

set BUserAccounts=1
set BDisWinUpdates=1
set BDelWinApps=1
set BGroupPolicy=1
set BClerAllUsersDesktop=1
set BClearSoftiumProfile=1

:MyStart

:: ****************************************************************************************
:: Устанавливаем на компьютере правильное время
::
:: Неправильно установленное время может вызывать ошибки в работе детей :-D !!!
::
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\SetNTPServers.bat"

:: ****************************************************************************************
:: Удалим всё с общего рабочего стола
:: ****************************************************************************************

if %BClerAllUsersDesktop%==1 (
	del "%SystemDrive%\Users\Public\Desktop\*" /q /s /f
	forfiles /P "%SystemDrive%\Users\Public\Desktop" /C "cmd /c (if @isdir==TRUE rmdir /q /s @file)"
	)

:: ****************************************************************************************
:: Скопируем файлы на настраиваемый компьютер
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\CopyFilesToPC.bat"

:: ****************************************************************************************
:: Установим программу Softium для обучающегося
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSoftiumscan.bat"

:: ****************************************************************************************
:: Настраиваем учётные записи пользователей на компьютере
:: ****************************************************************************************

if %BUserAccounts%==1 (
	CALL "%ScriptPath%Scripts\UserAccounts.bat"
	)

:: ****************************************************************************************
:: Запрещаем использование учётных записей Microsoft на компьютере
:: ****************************************************************************************
	CALL "%ScriptPath%Scripts\DisableMicrosoftAccounts.bat"

:: ****************************************************************************************
:: Отключим автоматическое обновление системы
:: ****************************************************************************************

if %BDisWinUpdates%==1 (
	CALL "%ScriptPath%Scripts\DisableWindowsUpdates.bat"
	)

:: ****************************************************************************************
:: Отключим службу поиска Windows.
:: Эта служба сильно тормозит ПК
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\DisableWindowsSearch.bat"

:: ****************************************************************************************
:: Включим на время работы скрипта режим электропитания "Высокая производительность"
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\HighPerformanceOn.bat"

:: ****************************************************************************************
:: Настроим дополнительные параметры электропитания
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\PowerSupplyParameters.bat"

:: ****************************************************************************************
:: Запланируем на всякий случай ежедневное выключение в 21:00
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\ScheduleShutdown.bat"

:: ****************************************************************************************
:: Включим административные папки
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\EnableAdministrativeFolders.bat"

:: ****************************************************************************************
:: Настроим межсетевой экран
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\FirewallSettings.bat"

:: ****************************************************************************************
:: Начинаем устанавливать все программы по очереди
:: ****************************************************************************************

:: Переходим на системный диск
%SystemDrive%

:: Начинаем ставить утилиты и программы

:: ****************************************************************************************
:: Устанавливаем SetuserFTA
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSetuserFTA.bat"

:: ****************************************************************************************
:: Устанавливаем BGInfo
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallBGInfo.bat"

:: ****************************************************************************************
:: Устанавливаем wget
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallWGET.bat"

:: ****************************************************************************************
:: Устанавливаем программу-архиватор 7zip и ассоциируем её с архивными файлами
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\Install7Zip.bat"
CALL "%ScriptPath%Scripts\7Zip-associate.bat"

:: ****************************************************************************************
:: Устанавливаем AdobeAIR + Scratch 2
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallScratch.bat"

:: ****************************************************************************************
:: Устанавливаем Scratch 3
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallScratch3.bat"

:: ****************************************************************************************
:: Устанавливаем GIMP
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallGIMP.bat"

:: ****************************************************************************************
:: Устанавливаем Google Chrome
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallGoogleChrome.bat"

:: ****************************************************************************************
:: Устанавливаем BABYTYPE2000
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallBABYTYPE2000.bat"

:: ****************************************************************************************
:: Устанавливаем Snap
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSnap.bat"

:: ****************************************************************************************
:: Устанавливаем Notepad++
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallNotepad++.bat"

:: ****************************************************************************************
:: Устанавливаем Construct
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallConstruct.bat"

:: ****************************************************************************************
:: Устанавливаем SWF_Player + Animate
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallAnimate.bat"

:: ****************************************************************************************
:: Устанавливаем Kodu
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallKodu.bat"

:: ****************************************************************************************
:: Устанавливаем Python 3
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallPython3.bat"

:: ****************************************************************************************
:: Устанавливаем ActivePresenter
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallActivePresenter.bat"

:: ****************************************************************************************
:: Отбражаем Мой компьютер
:: ****************************************************************************************

reg add "HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Explorer\HideDesktopIcons\NewStartPanel" /v "{20D04FE0-3AEA-1069-A2D8-08002B30309D}" /t REG_DWORD /d 0 /f

:: ****************************************************************************************
:: Создадим недостающие ярлыки
:: ****************************************************************************************

:: + Блокнот
	cscript /nologo /e:jscript "%SystemDrive%\ProgramData\Softium\lnk_create.js" "AllUsersDesktop"  "" "%windir%\system32\notepad.exe" "C:\Users\Softium\Documents" "Блокнот" "%windir%\system32\notepad.exe" "Текстовый редактор Блокнот"

:: + Скрипт выключения
	cscript /nologo /e:jscript "%SystemDrive%\ProgramData\Softium\lnk_create.js" "AllUsersDesktop"  "" "%SystemDrive%\ProgramData\Softium\shutdown.bat" "C:\Users\Softium\Documents" "Выключить" "%SystemRoot%\System32\SHELL32.dll,27" "Выключение компьютера"

:: ****************************************************************************************
:: Настройки для Microsoft Edge
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\DeleteMicrosoftEdge.bat"

:: ****************************************************************************************
:: Удалим One Drive
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\DeleteOneDrive.bat"

:: ****************************************************************************************
:: Удалим "лишние" программы Windows
:: ****************************************************************************************

if %BDelWinApps%==1 (
	start "Title" /wait powershell -command "Set-ExecutionPolicy Bypass -Force"
	start "Title" /wait powershell -File  "%SystemDrive%\ProgramData\Softium\DelWindowsApps.ps1"
	)

:: ****************************************************************************************
:: Отключаем режим планшета
:: ****************************************************************************************

reg add "HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\ImmersiveShell" /v TabletMode /t REG_DWORD /d 0 /f
reg add "HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\ImmersiveShell" /v SignInMode /t REG_DWORD /d 2 /f

:: ****************************************************************************************
:: Установим собственную тему оформления профиля пользователя и экрана приветсвия Windows
:: ****************************************************************************************

	CALL "%ScriptPath%Scripts\SetLockScreen.bat"
	CALL "%ScriptPath%Scripts\SetSoftiumTheme.bat"

:: ****************************************************************************************
:: включим режим электропитания "Экономия энергии"
:: ****************************************************************************************

	powercfg /setactive a1841308-3541-4fab-bc81-f71556f20b4a

:: ****************************************************************************************
:: Установим имя компьютера
:: ****************************************************************************************

	set /p MyHostname="Укажите имя компьютера: "
	wmic computersystem where name="%computername%" call rename name="%MyHostname%"

:: ****************************************************************************************
:: Установим параметры групповой политики
:: ****************************************************************************************

if %BGroupPolicy%==1 (
	"%ProgramFiles%\7-Zip\7z.exe" x -y  "%SystemDrive%\ProgramData\Softium\GroupPolicy.7z" -o"%windir%\System32"
	)

ECHO .
ECHO Всё!
ECHO .
ECHO Перезагрузка через 10 секунд...
	
:END

timeout 10 /nobreak

shutdown -r -f -t 00

EXIT /B
