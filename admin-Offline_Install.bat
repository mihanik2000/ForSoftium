@echo off
:: ****************************************************************************************
:: Редактировать данный скрипт рекомендуется в программе Notepad++
::
:: Автор скрипта Михаил Медведев aka mihanik
::
:: https://mihanik.net
::
::        Требуется наличие прав администратора: ДА
:: Антивирусная программа должна быть отключена: желательно, но не обязательно
::                                    Замечания: НЕТ
::
:: Описание
::
:: Скрипт установки пакета основных программ на компьютеры Softium.
:: 
:: Это основной скрипт.
:: ====================
:: Если этому скрипту требуется выполнить некоторые действия или установить какую-то программу
:: он вызывает соотвествующий скрипт из папки ".\Scripts"
::
:: Если вам не требуется устанавливать какие-то программы или нет необходимости выполнять некоторые
:: действия по настройке компьютера, просто удалите или задокументируйте часть скрипта, которая это делает.
::
:: ****************************************************************************************

:: Установим значение переменной ScriptPath.
:: Это путь к папке, откуда запущен этот скрипт.
:: Без этой переменной другие скрипты работать не будут!

set ScriptPath=%~dp0

:: ****************************************************************************************

:: Проверяем что настройка работы скрипта выполнена.
CALL "%ScriptPath%admin-Offline_Install.conf.bat"
 
if %SettingsEnabled%==0 (

	cscript //nologo "%ScriptPath%Scripts\MsgBox.vbs" ^
			"Для продолжения:`n`n1. Откройте файл admin-Offline_Install.conf.bat`n2. Задайте необходимые параметры.`n3. Запустите скрипт повторно." ^
			16 ^
			"Требуется настройка!"
	exit /b
)

:: ****************************************************************************************

:: Проверяем наличие у пользователя админских прав.
:: Если таковых прав нет, завершаем работу скрипта.

SET HasAdminRights=0
FOR /F %%i IN ('WHOAMI /PRIV /NH') DO (
	IF "%%i"=="SeTakeOwnershipPrivilege" SET HasAdminRights=1
)

IF NOT %HasAdminRights%==1 (

	cscript //nologo "%ScriptPath%Scripts\MsgBox.vbs" ^
			"Запустите скрипт с повышенными правами.`n`nДля этого:`n`n1. Кликните по скрипту правой кнопкой мыши`n2. Выберите пункт 'Запуск от имени Администратора'`n3. При необходимости введите пароль." ^
			16 ^
			"Недостаточно прав!"
	exit /b
)

mode 128,32
color 1f
cls

:: ****************************************************************************************
:: Включаем теневое копирование для системного диска.

if %BEnableSystemRestore%==1 (
	CALL "%ScriptPath%Scripts\EnableSystemRestore.bat"
)

:: ****************************************************************************************
:: Создаём точку восстановления Windows.

if %BEnableSystemRestore%==1 (
	CALL "%ScriptPath%Scripts\CreateSystemRestorePoint.bat"
)

:: ****************************************************************************************
:: Устанавливаем на компьютере правильное время
::
:: Неправильно установленное время может вызывать ошибки в работе детей :-D !!!
::
:: ****************************************************************************************

if %BSetNTPServers%==1 (
	CALL "%ScriptPath%Scripts\SetNTPServers.bat"
)

:: ****************************************************************************************
:: Удалим всё с общего рабочего стола
:: ****************************************************************************************

if %BClerAllUsersDesktop%==1 (
	del "%SystemDrive%\Users\Public\Desktop\*" /q /s /f
	forfiles /P "%SystemDrive%\Users\Public\Desktop" /C "cmd /c (if @isdir==TRUE rmdir /q /s @file)"
)

:: ****************************************************************************************
:: Скопируем файлы на настраиваемый компьютер
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\CopyFilesToPC.bat"

:: ****************************************************************************************
:: Установим программу Softium для обучающегося
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSoftiumscan.bat"

:: ****************************************************************************************
:: Настраиваем учётные записи пользователей на компьютере
:: ****************************************************************************************

if %BUserAccounts%==1 (
	CALL "%ScriptPath%Scripts\UserAccounts.bat"
)

:: ****************************************************************************************
:: Запрещаем использование учётных записей Microsoft на компьютере
:: ****************************************************************************************
if %BDisableMicrosoftAccounts%==1 (
	CALL "%ScriptPath%Scripts\DisableMicrosoftAccounts.bat"
)

:: ****************************************************************************************
:: Отключим автоматическое обновление системы
:: ****************************************************************************************

if %BDisWinUpdates%==1 (
	CALL "%ScriptPath%Scripts\DisableWindowsUpdates.bat"
	)

:: ****************************************************************************************
:: Отключим службу поиска Windows.
:: Эта служба сильно тормозит ПК
:: ****************************************************************************************

if %BDisableWindowsSearch%==1 (
	CALL "%ScriptPath%Scripts\DisableWindowsSearch.bat"
)

:: ****************************************************************************************
:: Включим на время работы скрипта режим электропитания "Высокая производительность"
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\HighPerformanceOn.bat"

:: ****************************************************************************************
:: Настроим дополнительные параметры электропитания
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\PowerSupplyParameters.bat"

:: ****************************************************************************************
:: Запланируем на всякий случай ежедневное выключение в 21:00
:: ****************************************************************************************

if %BScheduleShutdown%==1 (
	CALL "%ScriptPath%Scripts\ScheduleShutdown.bat"
)

:: ****************************************************************************************
:: Включим административные папки
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\EnableAdministrativeFolders.bat"

:: ****************************************************************************************
:: Настроим межсетевой экран
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\FirewallSettings.bat"

:: ****************************************************************************************
:: Начинаем устанавливать все программы по очереди
:: ****************************************************************************************

:: Переходим на системный диск
%SystemDrive%

:: Начинаем ставить утилиты и программы

:: ****************************************************************************************
:: Устанавливаем SetuserFTA
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSetuserFTA.bat"

:: ****************************************************************************************
:: Устанавливаем BGInfo
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallBGInfo.bat"

:: ****************************************************************************************
:: Устанавливаем wget
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallWGET.bat"

:: ****************************************************************************************
:: Устанавливаем программу-архиватор 7zip и ассоциируем её с архивными файлами
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\Install7Zip.bat"
CALL "%ScriptPath%Scripts\7Zip-associate.bat"

:: ****************************************************************************************
:: Устанавливаем AdobeAIR + Scratch 2
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallScratch.bat"

:: ****************************************************************************************
:: Устанавливаем Scratch 3
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallScratch3.bat"

:: ****************************************************************************************
:: Устанавливаем GIMP
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallGIMP.bat"

:: ****************************************************************************************
:: Устанавливаем Google Chrome
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallGoogleChrome.bat"

:: ****************************************************************************************
:: Устанавливаем BABYTYPE2000
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallBABYTYPE2000.bat"

:: ****************************************************************************************
:: Устанавливаем Snap
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallSnap.bat"

:: ****************************************************************************************
:: Устанавливаем Notepad++
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallNotepad++.bat"

:: ****************************************************************************************
:: Устанавливаем Construct
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallConstruct.bat"

:: ****************************************************************************************
:: Устанавливаем SWF_Player + Animate
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallAnimate.bat"

:: ****************************************************************************************
:: Устанавливаем Kodu
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallKodu.bat"

:: ****************************************************************************************
:: Устанавливаем Python 3
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallPython3.bat"

:: ****************************************************************************************
:: Устанавливаем ActivePresenter
:: ****************************************************************************************

CALL "%ScriptPath%Scripts\InstallActivePresenter.bat"

:: ****************************************************************************************
:: Отбражаем Мой компьютер
:: ****************************************************************************************

echo.
echo Отбражаем ярлык "Мой компьютер"
echo.

reg add "HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Explorer\HideDesktopIcons\NewStartPanel" /v "{20D04FE0-3AEA-1069-A2D8-08002B30309D}" /t REG_DWORD /d 0 /f >nul 2>&1

:: ****************************************************************************************
:: Создадим недостающие ярлыки
:: ****************************************************************************************

echo.
echo Создадим недостающие ярлыки.
echo.

:: + Блокнот
	cscript /nologo /e:jscript "%SystemDrive%\ProgramData\Softium\lnk_create.js" "AllUsersDesktop"  "" "%windir%\system32\notepad.exe" "C:\Users\Softium\Documents" "Блокнот" "%windir%\system32\notepad.exe" "Текстовый редактор Блокнот" >nul 2>&1

:: + Скрипт выключения
	cscript /nologo /e:jscript "%SystemDrive%\ProgramData\Softium\lnk_create.js" "AllUsersDesktop"  "" "%SystemDrive%\ProgramData\Softium\shutdown.bat" "C:\Users\Softium\Documents" "Выключить" "%SystemRoot%\System32\SHELL32.dll,27" "Выключение компьютера"  >nul 2>&1

:: ****************************************************************************************
:: Настройки для Microsoft Edge
:: ****************************************************************************************

echo.
echo Выполним некоторые настройки для Microsoft Edge
echo.

CALL "%ScriptPath%Scripts\DeleteMicrosoftEdge.bat"

:: ****************************************************************************************
:: Удалим One Drive
:: ****************************************************************************************

echo.
echo Удалим One Drive
echo.

CALL "%ScriptPath%Scripts\DeleteOneDrive.bat"

:: ****************************************************************************************
:: Удалим "лишние" программы Windows
:: ****************************************************************************************

echo.
echo Удалим "лишние" программы Windows 
echo.

if %BDelWinApps%==1 (
	start "Title" /wait powershell -command "Set-ExecutionPolicy Bypass -Force"
	start "Title" /wait powershell -File  "%SystemDrive%\ProgramData\Softium\DelWindowsApps.ps1"
	)

echo.
echo Готово
echo.

:: ****************************************************************************************
:: Отключаем режим планшета
:: ****************************************************************************************

echo.
echo Отключаем режим планшета
echo.

reg add "HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\ImmersiveShell" /v TabletMode /t REG_DWORD /d 0 /f >nul 2>&1
reg add "HKEY_CURRENT_USER\SOFTWARE\Microsoft\Windows\CurrentVersion\ImmersiveShell" /v SignInMode /t REG_DWORD /d 2 /f >nul 2>&1

:: ****************************************************************************************
:: Установим собственную тему оформления профиля пользователя и экрана приветсвия Windows
:: ****************************************************************************************

echo.
echo Установим собственную тему оформления профиля пользователя и экрана приветсвия Windows
echo.

CALL "%ScriptPath%Scripts\SetLockScreen.bat"
CALL "%ScriptPath%Scripts\SetSoftiumTheme.bat"

:: ****************************************************************************************
:: включим режим электропитания "Экономия энергии"
:: ****************************************************************************************

echo.
echo включим режим электропитания \"Экономия энергии\"
echo.

powercfg /setactive a1841308-3541-4fab-bc81-f71556f20b4a >nul 2>&1

:: ****************************************************************************************
:: Установим параметры групповой политики
:: ****************************************************************************************

if %BGroupPolicy%==1 (
	echo.
	echo Установим ограничение запуска программ из профиля пользователя...
	echo.

	"%ProgramFiles%\7-Zip\7z.exe" x -y  "%SystemDrive%\ProgramData\Softium\GroupPolicy.7z" -o"%windir%\System32"
	)

:: ****************************************************************************************
:: Создаём точку восстановления Windows.

if %BEnableSystemRestore%==1 (
	CALL "%ScriptPath%Scripts\CreateSystemRestorePoint.bat"
)

:: ****************************************************************************************
:: Установим имя компьютера
:: ****************************************************************************************

	set /p MyHostname="Укажите имя компьютера: "
	wmic computersystem where name="%computername%" call rename name="%MyHostname%" >nul 2>&1

echo.
echo Всё!
echo.
echo Перезагрузка через 10 секунд...
	
:END

timeout 10 /nobreak

shutdown -r -f -t 00

EXIT /B
